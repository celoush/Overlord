
;-------- TASK 9 (Find Mi-17,secondary) ------------------

@status2 == "DONE"
;-----------SERVER--------------------
? not local server :goto "random"
~ (random 60 + 30)
Task9Wait = true
publicvariable "Task9Wait"

;-----------GLOBAL--------------------
#random
@Task9Wait
player say "telegraph"
"9"objStatus "ACTIVE" 
~1
hint "Find crashed Mi-17 and take secret documents"
[resistance,"HQ"] sideChat "***** Mi-17 with secret documents was shot down above Bagango *****"
~5
[resistance,"HQ"] sideChat "***** New secondary mission: *****"
~1
[resistance,"HQ"] sideChat "***** Find crashed Mi-17 and take secret documents *****"
~1
hint "Pilot has secret documents. You should find him sooner than russians"

"Mi17Crash" setMarkerPos [14704,13809]

;-----------SERVER--------------------
? not local server :goto "konec"

_i = random 10
_i = round _i

? _i == 0 : _crash= [13393.7,13863.6,0.01]
? _i == 1 : _crash= [16592.7,14009.8,0.01]
? _i == 2 : _crash= [16173.8,14114.4,0.01]
? _i == 3 : _crash= [15824.9,13843.2,0.01]
? _i == 4 : _crash= [14964.5,13889.9,0.01]
? _i == 5 : _crash= [14241.4,14047.4,0.01]
? _i == 6 : _crash= [14480.6,13632.1,0.01]
? _i == 7 : _crash= [15700,13510.9,0.01]
? _i == 8 : _crash= [16029.8,13470.5,0.01]
? _i == 9 : _crash= [15425.4,13419.5,0.01]
? _i == 10 : _crash= [15351.5,14075.7,0.01]

;----cheat-----------------
;"kde" setMarkerPos _crash
;--------------------------


;--------------MI-17------------------------------------------------------
mi17 = "mi17_mg" createVehicle _crash
fire setpos [(_crash select 0)+4,(_crash select 1)-3,0.01]

"soldierEPilot" createUnit [[(_crash select 0)+5,(_crash select 1)-4,0.01],rus,"documents = this addaction [""Take documents"",""documents.sqs""],PilotDocument = this"]
"soldierEPilot" createUnit [[(_crash select 0)+6,(_crash select 1)-5,0.01],rus,"this setdammage 1"]

PilotDocument setdammage 0.8
PilotDocument disableAI "move"
mi17 setfuel 0
mi17 setdammage 0.6
~1
crash = _crash
[]exec "SmokeGranades.sqs"

;---------------- GLOBAL ------------------------------------------------
#konec
[]exec "UAZrescue.sqs"

@(status9 == "DONE" or status9 == "FAILED")

"9"objStatus "HIDDEN" 

?status9 == "DONE": titleText ["M I S S I O N   C O M P L E T E ","PLAIN",2]; [resistance,"HQ"] sideChat format ["***** Good job, %1 has found secret documents from crashed Mi-17 *****",meno2];playsound "fanfare"

?status9 == "FAILED": titleText ["M I S S I O N   F A I L E D ","PLAIN",2]; [resistance,"HQ"] sideChat "***** Shit, we dont have secret documents  *****"
"Mi17Crash" setMarkerPos [0,0]
PilotDocument removeaction documents
uaz1Crash removeaction documents2
uaz2Crash removeaction documents2
exit 
